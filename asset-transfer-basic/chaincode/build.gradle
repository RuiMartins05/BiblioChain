/*
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    id 'com.gradleup.shadow' version '8.3.5'
    id 'application'
    id 'jacoco'
}

group 'org.hyperledger.fabric.samples'
version '1.0-SNAPSHOT'

dependencies {

    implementation 'org.hyperledger.fabric-chaincode-java:fabric-chaincode-shim:2.5.+'
    implementation 'org.json:json:+'
    implementation 'com.owlike:genson:1.6'
}

repositories {
    mavenCentral()
    maven {
        url 'https://jitpack.io'
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

application {
    mainClass = 'org.hyperledger.fabric.contract.ContractRouter'
}

jacocoTestReport {
    dependsOn test
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }

    finalizedBy jacocoTestReport
}

mainClassName = 'org.hyperledger.fabric.contract.ContractRouter'

shadowJar {
    archiveBaseName = 'chaincode'
    archiveVersion = ''
    archiveClassifier = ''
    mergeServiceFiles()

    manifest {
        attributes 'Main-Class': 'org.hyperledger.fabric.contract.ContractRouter'
    }
}

check.dependsOn jacocoTestCoverageVerification
installDist.dependsOn check
